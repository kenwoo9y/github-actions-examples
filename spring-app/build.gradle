plugins {
	id 'java'
	id 'org.springframework.boot' version '3.3.5'
	id 'io.spring.dependency-management' version '1.1.6'
	id 'jacoco'
	id 'com.diffplug.spotless' version '6.23.0'
	id 'checkstyle'
}

group = 'com.example'
version = '0.0.1-SNAPSHOT'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(17)
	}
}

repositories {
	mavenCentral()
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-web'
	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'
	developmentOnly 'org.springframework.boot:spring-boot-devtools'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

tasks.named('test') {
	useJUnitPlatform()
	finalizedBy jacocoTestReport
}

spotless {
    java {
        googleJavaFormat()  // Google Java Format を使用
    }
}

checkstyle {
    toolVersion = '10.20.1' // 使用したいCheckstyleのバージョン
    configFile = file("${rootProject.projectDir}/config/checkstyle/checkstyle.xml")
}

tasks.withType(Checkstyle) {
    reports {
        xml.required.set(false)
        html.required.set(true)
    }
}

jacoco {
	toolVersion = "0.8.7"
}

jacocoTestReport {
	dependsOn test
	reports {
		xml.required = true
		html.required = true
	}
	// 除外するファイルやパッケージを指定
    classDirectories.setFrom(
        files(classDirectories.files.collect {
            fileTree(dir: it, exclude: [
                'com/example/spring_app/SpringAppApplication.class'
            ])
        })
    )
}

jacocoTestCoverageVerification {
	// 除外するファイルやパッケージを指定
    classDirectories.setFrom(
        files(classDirectories.files.collect {
            fileTree(dir: it, exclude: [
                'com/example/spring_app/SpringAppApplication.class'
            ])
        })
    )

	violationRules {
		rule {
			limit {
				minimum = 0.8  // 80%未満の場合、ジョブを失敗させる
			}
		}
	}
}

check.dependsOn jacocoTestCoverageVerification